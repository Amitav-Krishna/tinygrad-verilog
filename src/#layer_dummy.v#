module layer #(
		parameter N = 4,
		parameter M = 4
	       ) (
		   input wire		      clk,
		   input wire		      start,
		   input wire signed [(N*16)-1:0]     x,
		   input wire signed [((M*N)*16)-1:0] w,
		   input wire signed [(M*16)-1:0]  b,
		   output wire signed [(M*16)-1:0] y,
		   output reg done
		  );
   reg [$clog2(N+1)-1:0] count; 
   wire signed [(M*16)-1:0] activations;
   reg signed [15:0]	    x_val;
   reg signed [(M*16)-1:0] w_vals;
   
   genvar	    i;

   assign y = activations;


   generate
      for (i = 0; i < M; i = i + 1) begin
	 neuron #(.N(N)) neuron_inst (
				      .clk(clk),
				      .start(start),
				      .x(x_val),
		      .w(w_vals[
				((i+1)*16)-1:
				(i*16)		
				]),
		      .b(b[((i+1)*16)-1:(i*16)]),
	      .activation(activations[((i+1)*16)-1:(i*16)]),
	      .done()
	      );
      end // for (i = 0; i < M; i++)
   endgenerate

   always @(posedge clk) begin
      if (start) begin
 	 count <= 0;
 	 x_val <= x[0 +: 16];
 	 w_vals <= w[0 +: (M*16)];
 	 done <= (N == 1);
      end else if (count < N - 1) begin
 	 count <= count + 1;
 	 x_val <= x[(((count + 1) * 16)) +: 16];
 	 w_vals <= w[(((count + 1) * M * 16)) +: (M*16)];
 	 done <= (count + 1 == N - 1);
      end else if (done) begin
 	 done <= 0;
      end
   end
	 
endmodule // layer

